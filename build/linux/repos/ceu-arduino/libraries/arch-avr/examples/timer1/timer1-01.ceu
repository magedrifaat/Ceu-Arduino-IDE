// compile with CEU_DEFS=-DCEU_PM_MIN

/*
 * Runs a timer on CTC mode, e.g., every 500ms generates an interrupt.
 * Interrupt generates "emit 500ms" to the application.
 * Main program blinks LED connected to PIN13 every 500ms.
 */

native/const _TIMER1_COMPA_vect;

// initialize timer1 
{
    TCCR1A = 0;
    TCCR1B = 0;
    TCNT1  = 0;
    OCR1A  = 31250;                  // compare match register 16MHz/256/2Hz
    TCCR1B = TCCR1B | (1 << WGM12);  // CTC mode
    TCCR1B = TCCR1B | (1 << CS12);   // 256 prescaler
    TIMSK1 = TIMSK1 | (1 << OCIE1A); // enable timer compare interrupt
}

input none DT;

spawn async/isr [_TIMER1_COMPA_vect, 0] do
    emit DT;    // 500ms
end

_pinMode(13, _OUTPUT);
var high/low x = low;
every DT do
    x = not x;
    _digitalWrite(13, x);
end
